buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath('com.github.triplet.gradle:play-publisher:1.1.5') {
            exclude group: 'com.google.guava'
        }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

play {
    uploadImages = false
    serviceAccountEmail =
            '925691948433-pl4bec638njeenipednmsmo0q55j6907@developer.gserviceaccount.com'
    pk12File = file('play_api_client.p12')
}

repositories {
    maven {
        url 'https://raw.githubusercontent.com/felixb/mvn-repo/master'
    }
    maven {
        url "https://jitpack.io"
    }
    mavenCentral()
}

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionName "3.11.13"
        versionCode 1403111300
        buildConfigField 'boolean', 'DEBUG_LOG', 'true'
    }

    lintOptions {
        // checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }


    signingConfigs {
        release {
            File releaseProps = new File("release.properties")
            if (releaseProps.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(releaseProps))

                storeFile file("../release.ks")
                storePassword props.getProperty("releaseKeystorePassword")
                keyAlias "jarsign"
                keyPassword props.getProperty("releaseKeyPassword")
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }

        nologDebug {
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            minifyEnabled true
            buildConfigField 'boolean', 'DEBUG_LOG', 'false'
        }

        logRelease {
            versionNameSuffix "-logRelease"
            signingConfig signingConfigs.release
            minifyEnabled false
        }

        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            minifyEnabled true
            buildConfigField 'boolean', 'DEBUG_LOG', 'false'
        }
    }
}


dependencies {
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.android.gms:play-services-ads:10.2.4'
    compile 'com.github.hackware1993:MagicIndicator:1.5.0'
    compile 'de.ub0r.android.lib:lib:1.1.1'
    compile 'de.ub0r.android.logg0r:logg0r:2.0.0'
    compile 'de.ub0r.android.eucookieconsent:library:0.2.0@aar'
}

task updateChangelog << {
    def proc = 'shell/update-changelog.sh'.execute()
    proc.in.eachLine {line -> println line}
    proc.err.eachLine {line -> println 'ERROR: ' + line}
    proc.waitFor()
}

task removeUnfinishedTranslations << {
    def proc = 'shell/remove-unfinished-translations.sh'.execute()
    proc.in.eachLine {line -> println line}
    proc.err.eachLine {line -> println 'ERROR: ' + line}
    proc.waitFor()
}

task readdUnfinishedTranslations << {
    def proc = "git checkout CallMeter3G/src/main/res/values-*/*xml".execute()
    proc.in.eachLine {line -> println line}
    proc.err.eachLine {line -> println 'ERROR: ' + line}
    proc.waitFor()
}

tasks.whenTaskAdded { task ->
    if (task.name == 'mergeReleaseResources') {
        task.dependsOn updateChangelog
        task.dependsOn removeUnfinishedTranslations
    }
}
